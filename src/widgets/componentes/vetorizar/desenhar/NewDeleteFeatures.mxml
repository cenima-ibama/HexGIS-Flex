<?xml version="1.0" encoding="utf-8"?>
<os:IconButton xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:os="org.openscales.fx.control.*"
			   implements="org.openscales.core.control.IControl"
			   toolTip="{this.tooltip}"
			   icon="{_btnDelete}" alpha="{_alpha}"
			   width="15" height="15"
			   click="onClick()" enabled="{active}">
	
	<!-- rollOver="itemRollOver(event)" rollOut="itemRollOut(event)"-->
	
	<fx:Script>
		<![CDATA[
			import org.openscales.core.Map;
			import org.openscales.core.control.IControl;
			import org.openscales.core.events.DrawingEvent;
			import org.openscales.core.events.FeatureEvent;
			import org.openscales.core.events.I18NEvent;
			import org.openscales.core.feature.Feature;
			import org.openscales.core.feature.PointFeature;
			import org.openscales.core.handler.IHandler;
			import org.openscales.core.handler.feature.DragFeatureHandler;
			import org.openscales.core.handler.feature.SelectFeaturesHandler;
			import org.openscales.core.i18n.Catalog;
			import org.openscales.core.layer.VectorLayer;
			import org.openscales.fx.control.layer.LayerManager;
			import org.openscales.geometry.Point;
			import org.openscales.geometry.basetypes.Location;
			import org.openscales.geometry.basetypes.Pixel;
			
			[Bindable]
			private var _alpha:Number = 0.5;
			
			[Bindable]
			private var tooltip:String = Catalog.getLocalizationForKey('drawing.delete');
			
			[Bindable]
			[Embed(source="/assets/images/icons/black/7.png")]
			private var _btnDelete:Class;
			
			private var _target:VectorLayer;
			
			[Bindable]
			private var _map:Map;
			private var _active:Boolean = true;
			
			[Bindable]
			private var _handler:SelectFeaturesHandler;
			private var _isClick:Boolean = false;
			private var _drawingMode:String;
			
			/**
			 * Indicates for Drawing mode
			 * multiple : tools are in the LayerSwitcher
			 * unique : tools are in the DrawingToolbar
			 */
			[Bindable]
			public function get drawingMode():String
			{	
				return _drawingMode;
			}
			
			public function set drawingMode(value:String):void
			{
				_drawingMode = value;
			}
			
			/**
			 * Flag for activating and deactivating the component
			 * @default true;
			 */
			[Bindable]
			public function get active():Boolean
			{
				return _active;
			}
			public function set active(value:Boolean):void
			{
				_active = value;
				if(! _active)
				{
					_handler.active = false;
				}
			}
			
			/**
			 * Map controled by this control
			 */
			public function get map():Map
			{
				return _map;
			}
			public function set map(value:Map):void
			{
				this._map = value;
				this._map.addEventListener(DrawingEvent.DRAW_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.EDIT_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.MOVE_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.DELETE_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.SELECT_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.CHANGE_ACTIVE_LAYER, onChangeActiveLayer);
				this._map.addEventListener(I18NEvent.LOCALE_CHANGED, onMapLanguageChange);
				this._map.addEventListener(FeatureEvent.FEATURE_SELECT, onSelectedFeature);
			}
			
			/**
			 * Position (x,y) of the handler
			 */
			public function get position():Pixel
			{
				return new Pixel(x,y);
			}
			public function set position(value:Pixel):void
			{
				if(value)
				{
					x = value.x;
					y = value.y;
				}
			}
			
			/**
			 * Layer the features will be moved
			 */
			public function get target():VectorLayer
			{
				return _target;
			}
			
			public function set target(value:VectorLayer):void
			{
				_target = value;
			}
			
			/**
			 * To activate or disactivate the handler
			 */
			private function activate():void
			{
				_handler.active = true;
				_handler.map.mouseNavigationEnabled = true;
				_handler.map.panNavigationEnabled = true;
				_handler.map.zoomNavigationEnabled = true;
				_handler.map.keyboardNavigationEnabled = true;
				_alpha = 1;
			}
			private function disactivate():void
			{
				_handler.active = false;
				_handler = null;
				_alpha = 0.5;
			}
			
			/**
			 * This function is called when a feature is selected
			 */
			private function onSelectedFeature(event:FeatureEvent):void
			{
				if(!this.target)
					return;
				if(event.feature.layer != this.target)
					return;
				if(this._handler && this._handler.active)
				{
					this.target.removeFeature(event.feature, true);
				}
			}
			
			/**
			 * On button click, dispatch a DrawingEvent.EDIT_HANDLER_ACTIVATED event
			 */
			public function onClick():void
			{
				if(!this.target)
					return;
				
				// Disactive this component
				var evt:DrawingEvent;
				
				if(this._handler)
				{
					disactivate();
					return;
				} 
				else 
				{
					// Add a new SelectFeaturesHandler
					this._handler = new SelectFeaturesHandler(this._map,true,true,false,false);
					this._handler.clickOut = false;
					this._handler.toggle = false;
					this._handler.redrawFeatureOnSelection = false;
					this._handler.layers[0] = _target;
				}
				
				// Disactive others drawing tools components
				this._isClick = true;
				evt = new DrawingEvent(DrawingEvent.DELETE_HANDLER_ACTIVATED);
				evt.activeHandler = this.id;
				evt.layer = this.target;
				this._map.dispatchEvent(evt);
				
				// Disactive mesure tools components
				evt = new DrawingEvent(DrawingEvent.CHANGE_ACTIVE_HANDLER);
				evt.activeHandler = null;
				this._map.dispatchEvent(evt);
				
				// Active this component
				activate();
			}
			
			/* private function itemRollOver(event:MouseEvent):void
			{
				_alpha = 1.0;
			}
			
			private function itemRollOut(event:MouseEvent):void
			{				
				if (!this.active)
				{
					_alpha = 0.5;
				}
			} */
			
			/**
			 * Check the handler when a DrawingEvent.DRAW_HANDLER_ACTIVATED or
			 * DrawingEvent.EDIT_HANDLER_ACTIVATED or DrawingEvent.MOVE_HANDLER_ACTIVATED
			 * or DrawingEvent.SELECT_HANDLER_ACTIVATED event occured
			 */
			public function onChangeActiveHandler(evt:DrawingEvent=null):void
			{
				if(this._handler && !this._isClick)
					disactivate();
				else
					this._isClick = false;
			}
			
			/**
			 * Update the target on active layer changed
			 */
			private function onChangeActiveLayer(evt:DrawingEvent):void{
				
				if(this.drawingMode == 'unique')
				{
					if(this._handler)
					{
						disactivate();
					}
					this.target = evt.layer;
					
					if(!evt.layer || !evt.layer.editable)
					{
						this.buttonMode = false;
						this.useHandCursor = false;
						this.toolTip = Catalog.getLocalizationForKey('drawing.selectDrawingLayer');
					} 
					else
					{
						this.buttonMode = true;
						this.useHandCursor = true;
						this.toolTip = Catalog.getLocalizationForKey('drawing.delete');
					}
				}
			}
			
			/**
			 * Update texts on map language changed
			 */
			public function onMapLanguageChange(event:I18NEvent):void
			{
				this.tooltip = Catalog.getLocalizationForKey('drawing.delete');
			}
			
			// Unused functions
			public function draw():void
			{
				this.invalidateDisplayList();
			}
			public function destroy():void
			{
			}
			
		]]>
	</fx:Script>
	
	
</os:IconButton>
